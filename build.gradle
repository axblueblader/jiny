plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'application'
}

group 'com.tuhuynh'
version '0.1.9-ALPHA'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/huynhminhtufu/httpserver")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
}

ext {
    javaMainClass = 'com.tuhuynh.httpserver.tests.TestServer'
}

application {
    mainClassName = javaMainClass
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
}